#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = true
ScannerTable = Compact
Input = String
Parser = LL
#RegularDefinitions
dig: [0-9]
min: [a-z]
mai: [A-Z]
pos: [1-9]
num_opc: {dig}? {dig}? {dig}? {dig}?
bloco: %% [^%]* %%
linha: % [^\n]*
#Tokens

identificador:! ({mai}{min}|{min})+(_ {dig}+)?

pr_add       = identificador: "add"
pr_and       = identificador: "and"
pr_begin     = identificador: "begin"
pr_bool      = identificador: "bool"
pr_count     = identificador: "count"
pr_delete    = identificador: "delete"
pr_do        = identificador: "do"
pr_elementOf = identificador: "elementOf"
pr_else      = identificador: "else"
pr_end       = identificador: "end"
pr_false     = identificador: "false"
pr_float     = identificador: "float"
pr_if        = identificador: "if"
pr_int       = identificador: "int"
pr_list      = identificador: "list"
pr_not       = identificador: "not"
pr_or        = identificador: "or"
pr_print     = identificador: "print"
pr_read      = identificador: "read"
pr_size      = identificador: "size"
pr_string    = identificador: "string"
pr_true      = identificador: "true"
pr_until     = identificador: "until"

// Constantes
cint:   0|{pos}{num_opc}
cfloat: (0|{pos}{num_opc})\.({num_opc}{pos} | 0)
cstring: \"[^\n\"\\]*\"

// Comentários
:! {bloco}
:!{linha}

// Símbolos
"+"
"-"
"*"
"/"
"=="
"~="
"<"
">"
"="
"<-"
"("
")"
";"
","

// Especiais
:[\s\t\n\r]

#NonTerminals
<gramatica>
<lista_instrucoes>
<lista_instrucoes_opc>
<instrucao>
<comando>
<declaracao_variaveis>
<tipo_primitivo>
<tipo>
<lista_identificadores>
<lista_identificadores_opc>
<atribuicao_diferente>
<comando_atribuicao_operacao_lista>
<entrada>
<parametro_entrada>
<lista_entrada>
<lista_entrada_opc>
<saida>
<lista_expressoes>
<lista_expressoes_opc>
<condicional>
<else_opc>
<lista_comandos>
<lista_comandos_opc>
<repeticao>
<operacao_lista>
<expressao>
<expressao_>
<valor>
<relacional>
<relacional_>
<operador_relacional>
<aritmetica>
<aritmetica_>
<termo>
<termo_>
<fator>
<fator_>

#Grammar
<gramatica> ::= #100 pr_begin <lista_instrucoes> pr_end #101;

<lista_instrucoes> ::= <instrucao> ";" <lista_instrucoes_opc> ;
<lista_instrucoes_opc> ::= <lista_instrucoes> | î ;

<instrucao> ::= <comando> | <declaracao_variaveis> ;

<comando> ::= identificador #121 <comando_atribuicao_operacao_lista>
            | <entrada>
            | <saida>
            | <condicional>
            | <repeticao>
            ;
<comando_atribuicao_operacao_lista> ::= <atribuicao_diferente> | <operacao_lista> ;

<declaracao_variaveis> ::= <tipo> <lista_identificadores> #119 ;
<tipo_primitivo> ::= pr_int #120 | pr_float #120 | pr_bool #120 | pr_string #120 ;
<tipo> ::= <tipo_primitivo> | pr_list "(" <tipo_primitivo> "," cint ")" ;

<lista_identificadores> ::= identificador #121 <lista_identificadores_opc> ;
<lista_identificadores_opc> ::= "," <lista_identificadores> | î ;

<atribuicao_diferente> ::= "=" <expressao> #122 | "<-" <expressao> #122 ;

<operacao_lista> ::= pr_add "(" <expressao> "," <expressao> ")"
                   | pr_delete "(" <expressao> ")"
                   ;

<entrada> ::= pr_read "(" <lista_entrada> ")" ;
<parametro_entrada> ::= identificador #123 | cstring #124 ","  identificador #123 ;
<lista_entrada> ::= <parametro_entrada> <lista_entrada_opc> ;
<lista_entrada_opc> ::= "," <lista_entrada> | î ;

<saida> ::= pr_print "(" <lista_expressoes> ")" #118 ;
<lista_expressoes> ::= <expressao> #102 <lista_expressoes_opc> ;
<lista_expressoes_opc> ::= "," <lista_expressoes> | î ;

<condicional> ::= pr_if <expressao> #125 <lista_comandos> <else_opc> pr_end #126 ;
<lista_comandos> ::= <comando> ";" <lista_comandos_opc> ;
<lista_comandos_opc> ::= <lista_comandos> | î ;
<else_opc> ::= î | pr_else #127 <lista_comandos> ;

<repeticao> ::= pr_do #128 <lista_comandos> pr_until <expressao> #129 ;

<expressao> ::= <valor> <expressao_> ;
<expressao_> ::= î | pr_and <valor> #113 <expressao_> | pr_or <valor> #114 <expressao_> ;
<valor> ::= <relacional> | pr_true #115 | pr_false #116 | pr_not <valor> #117 ;
<relacional> ::= <aritmetica> <relacional_> ;
<relacional_> ::= î | <operador_relacional> #111 <aritmetica> #112 ;
<operador_relacional>::= "==" | "~=" | "<" | ">" ;
<aritmetica> ::= <termo> <aritmetica_> ;
<aritmetica_> ::= î | "+" <termo> #106 <aritmetica_> | "-" <termo> #107 <aritmetica_> ;
<termo> ::= <fator> <termo_> ;
<termo_> ::= î | "*" <fator> #108 <termo_> | "/" <fator> #109 <termo_> ;
<fator> ::= identificador #130 <fator_> |
	cint #103 |
	cfloat #104 |
	cstring #105 |
		"(" <expressao> ")" |
		"+" <fator> |
	    "-" <fator> #110
	    ;
<fator_> ::= î | pr_count | pr_size | pr_elementOf "(" <expressao> ")" ;

